#!/usr/bin/csi -ss
(use ncurses)
(use posix)
(require-extension srfi-13)

(define robot-row (make-parameter 1))
(define robot-col (make-parameter 1))

(define robot-row-prev (make-parameter 1))
(define robot-col-prev (make-parameter 1))

(define moved? (make-parameter #f))

(define chicken-row (make-parameter 1))
(define chicken-col (make-parameter 1))

(define (quit message code)
  (flushinp)
  (clear)
  (endwin)
  (unless (zero? code)
    (fprintf (current-error-port) message)
    (exit code))
  (print message)
  (exit))

(define (centre-message . messages)
  (define (centre-message-iter messages n)
    (when (pair? messages)
      (mvprintw (+ (quotient (LINES) 2) (- n (length messages)))
                (quotient (- (COLS) (string-length (car messages))) 2)
                (car messages))
      (centre-message-iter (cdr messages) (add1 n))))
  (centre-message-iter messages 0))

(define (random-row)
  (add1 (random (- (LINES) 2))))
(define (random-col)
  (add1 (random (- (COLS) 2))))

(define (draw-chicken)
  (attron (COLOR_PAIR 3))
  (mvaddch (chicken-row) (chicken-col) (ACS_CKBOARD))
  (attroff (COLOR_PAIR 3)))

(define (draw-robot)
  (mvprintw 3 3 "Old position: (~A, ~A)" (robot-row-prev) (robot-col-prev))
  (mvprintw 4 3 "New position: (~A, ~A)" (robot-row) (robot-col))
  (mvaddch (robot-row-prev) (robot-col-prev) #\ )
  (attron (COLOR_PAIR 2))
  (mvaddch (robot-row) (robot-col) (ACS_DIAMOND))
  (attroff (COLOR_PAIR 2))
  (moved? #f))

(define (move #!key (v 'nil) (h 'nil))
  (robot-row-prev (robot-row))
  (robot-col-prev (robot-col))
  (if (eq? v 'up)
      (robot-row (sub1 (robot-row)))
      (if (eq? v 'down)
          (robot-row (add1 (robot-row)))))
  (if (eq? h 'left)
      (robot-col (sub1 (robot-col)))
      (if (eq? h 'right)
          (robot-col (add1 (robot-col)))))
  (cond
   ((> (robot-row) (- (LINES) 2))
    (robot-row (- (LINES) 2)))
   ((> (robot-col) (- (COLS) 3))
    (robot-col (- (COLS) 3)))
   ((< (robot-row) 1)
    (robot-row 1))
   ((< (robot-col) 1)
    (robot-col 1))
   ((and (= (chicken-col) (robot-col))
         (= (chicken-row) (robot-row)))
    (rfc-win)))
  (moved? #t))

(define (rfc-frame)
  (attron (COLOR_PAIR 1))
  (box (stdscr) (ACS_VLINE) (ACS_HLINE))
  (attroff (COLOR_PAIR 1)))

(define (rfc-splash)
  (rfc-frame)

  (attron (COLOR_PAIR 4))
  (centre-message "You are the Robot!" "Find the Chicken!" "Godspeed!")
  (attroff (COLOR_PAIR 4))

  (attron (COLOR_PAIR 6))
  (mvprintw (- (LINES) 2) (- (COLS) 18) "Press Q to exit.")
  (mvprintw (- (LINES) 3) 2 "Use       to move.")
  (attroff (COLOR_PAIR 6))

  (attron (COLOR_PAIR 5))
  (mvprintw (- (LINES) 4) 6 "7 8 9")
  (mvprintw (- (LINES) 3) 6 "4   6")
  (mvprintw (- (LINES) 2) 6 "1 2 3")
  (attroff (COLOR_PAIR 5))

  (attron (COLOR_PAIR 4))
  (mvaddch (- (LINES) 3) 8 (ACS_PLUS))
  (attroff (COLOR_PAIR 4)))

(define (rfc-init)
  (initscr)

  (if (not (has_colors))
      (quit "Your terminal does not support colours." 1))
  
  (start_color)
  (init_pair 1 COLOR_BLUE COLOR_BLACK)
  (init_pair 2 COLOR_CYAN COLOR_BLACK)
  (init_pair 3 COLOR_RED COLOR_BLACK)
  (init_pair 4 COLOR_YELLOW COLOR_BLACK)
  (init_pair 5 COLOR_MAGENTA COLOR_BLACK)
  (init_pair 6 COLOR_GREEN COLOR_BLACK)

  (raw) (noecho)
  (keypad (stdscr) #t)
  (curs_set 0)

  (attron A_BOLD)

  (rfc-splash)
  
  (if (member (getch) '(#\q #\Q KEY_F0))
      (quit "That was quick." 0))

  (robot-row (random-row))
  (robot-col (random-col))
  (robot-row-prev (robot-row))
  (robot-col-prev (robot-col))
  (chicken-row (random-row))
  (chicken-col (random-col))

  (clear)
  (rfc-frame)
  (draw-robot)
  (draw-chicken)
  (rfc-loop))

(define (rfc-loop)
  (if (moved?) (draw-robot))

  (case (getch)
    ((#\q #\Q KEY_F0)
     (quit "You couldn't find the chicken." 0))
    ((#\8 KEY_UP)
     (move v: 'up))
    ((#\9)
     (move v: 'up
           h: 'right))
    ((#\6 KEY_RIGHT)
     (move h: 'right))
    ((#\3)
     (move v: 'down
           h: 'right))
    ((#\2 KEY_DOWN)
     (move v: 'down))
    ((#\1)
     (move v: 'down h: 'left))
    ((#\4 KEY_LEFT)
     (move h: 'left))
    ((#\7)
     (move v: 'up
           h: 'left)))

  (rfc-loop))

(define (rfc-win)
  (clear)
  (rfc-frame)
  (attron (COLOR_PAIR 4))
  (centre-message "You found the chicken!" "Bravo!")
  (attroff (COLOR_PAIR 4))
  (refresh)
  (sleep 3)
  (getch)
  (quit "Thanks for playing!" 0))

(define (main args)  
  (if (null? args)
      (rfc-init)
      (print "This program does not take options.")))
